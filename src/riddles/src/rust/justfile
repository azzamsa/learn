#!/usr/bin/env -S just --justfile

alias d := dev
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    cargo nextest --version || cargo install --locked cargo-nextest

# Develop the app.
dev pattern:
    cargo watch -x 'test -- --nocapture --test {{ pattern }}'

# Format the codebase.
fmt:
    cargo fmt --all

# Check is the codebase properly formatted.
fmt_check:
    cargo fmt --all -- --check

# Lint the docstring.
_lint_doc:
    cargo doc --all-features --no-deps

# Lint the codebase.
lint: _lint_doc
    cargo clippy

# Test the codebase.
test:
    cargo test --doc
    cargo nextest run

# Tasks to make the code-base comply with the rules. Mostly used in git hooks.
comply: fmt lint test

# Check if the repository comply with the rules and ready to be pushed.
check: fmt_check lint test

# Open documentation
doc:
    cargo doc --open

# Local Variables:
# mode: makefile
# End:
# vim: set ft=make :
