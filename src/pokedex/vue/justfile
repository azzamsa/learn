#!/usr/bin/env -S just --justfile

shebang := if os() == 'windows' { 'powershell.exe' } else { '/usr/bin/sh' }

alias r := run
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    pnpm i

# Develop the app.
run:
    pnpm dev

# Preview the app.
preview:
    pnpm preview

# Build the app.
build:
    pnpm run build

# Lint the codebase.
lint:
    pnpm run lint

# Format the codebase.
fmt:
    pnpm fun fmt
    dprint fmt --config configs/dprint.json

# Check is the codebase properly formatted.
fmt-check: lint
    dprint check --config configs/dprint.json

# Test the codebase.
test:
    pnpm run test

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint test

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint test

# Create a new release. Example `just release v2.2.0`
release version:
    bash scripts/release.sh {{ version }}

# Check dependencies health. Pass `--write` to uppgrade dependencies.
up arg="":
    #!{{ shebang }}
    if [ "{{ arg }}" = "--write" ]; then
        taze --write
    else
        taze
    fi;
