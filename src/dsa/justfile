#!/usr/bin/env -S just --justfile

alias d := dev
alias f := fmt
alias l := lint
alias t := test
alias c := comply
alias k := check

[doc('List available commands')]
_default:
    just --list --unsorted

[confirm('⚠️ This command will alter your system. Run recipe `setup`?')]
[doc('Setup the repository')]
setup:
    rye sync

[doc('Tasks to make the code-base comply with the rules. Mostly used in git hooks')]
comply: fmt lint test

[doc('Check if the repository comply with the rules and ready to be pushed')]
check: fmt-check lint test

[doc('Develop the app')]
dev:
    rye lint

[doc('Format the codebase.')]
fmt:
    rye fmt
    dprint fmt

[doc('Check is the codebase properly formatted')]
fmt-check:
    rye fmt --check
    dprint check

[doc('Lint the codebase')]
lint:
    rye lint
    typos

[doc('Test the codebase')]
test:
    rye test
