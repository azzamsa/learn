#!/usr/bin/env -S just --justfile

alias r := run
alias d := dev
alias t := test

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup: _areyousure
    rustup target add wasm32-unknown-unknown
    npm install

    just _cargo-install 'trunk microserver bacon'

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint test

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint test _doc-check

# Develop the app.
dev:
    bacon

# Run the app.
run:
    pnpx tailwindcss --config configs/tailwind.config.js --input styles/main.scss  --output styles/compiled.scss
    trunk serve

# Build the app.
build:
    NODE_ENV=production pnpx tailwindcss --config configs/tailwind.config.js --input styles/main.scss --output styles/compiled.scss
    trunk build --release

# Preview the build output.
preview-build:
    microserver dist/

# Format the codebase.
fmt:
    cargo fmt --all
    pnpx prettier --config configs/prettier.config.js --ignore-path configs/prettierignore --write .

# Check is the codebase properly formatted.
fmt-check:
    cargo fmt --check
    pnpx prettier --config configs/prettier.config.js --ignore-path configs/prettierignore --check .

# Lint the codebase.
lint:
    cargo clippy --all-targets --all-features

# Lint the codebase.
test:
    cargo test --all --all-features --all-targets

# Check the documentation.
_doc-check:
    cargo doc --all-features --no-deps

# Check dependencies health. Pass `--write` to uppgrade dependencies.
[unix]
up arg="":
    #!/usr/bin/env bash
    if [ "{{ arg }}" = "--write" ]; then
        cargo upgrade && cargo update
    else
        cargo outdated --root-deps-only
    fi;

[windows]
up arg="":
    #!powershell.exe
    if ( "tool" -eq "--write") {
        cargo upgrade
        cargo update
    }
    else {
        cargo outdated --root-deps-only
    }

#
# Helper
#

[unix]
_cargo-install tool:
    #!/usr/bin/env bash
    if command -v cargo-binstall >/dev/null 2>&1; then
        echo "cargo-binstall..."
        cargo binstall --no-confirm --no-symlinks {{ tool }}
    else
        echo "Building from source"
        cargo install --locked {{ tool }}
    fi

[unix]
_areyousure:
    #!/usr/bin/env bash
    echo -e "This command will alter your system. ⚠️
    You are advised to run in inside containerized environment.
    Such as [toolbx](https://containertoolbx.org/).

    If you are unsure. Run the installation commands manually.
    Take a look at the 'setup' recipe in the Justfile.\n"

    read -p "Are you sure you want to proceed? (Y/n) " response;
    if [[ $response =~ ^[Yy] ]]; then
        echo "Continue!";
    else
        echo "Cancelled!";
        exit 1;
    fi
