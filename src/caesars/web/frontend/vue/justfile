#!/usr/bin/env -S just --justfile

shebang := if os() == 'windows' { 'powershell.exe' } else { '/usr/bin/sh' }

alias d := dev

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    pnpm i

# Develop the app.
dev:
    pnpm dev

# Lint the codebase.
lint:
    pnpm lint

# Format the codebase.
fmt: lint

# Check is the codebase properly formatted.
fmt-check: lint

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint
    pnpm run type-check

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint
    pnpm run type-check

# Check dependencies health. Pass `--write` to uppgrade dependencies.
up arg="":
    #!{{ shebang }}
    if [ "{{ arg }}" = "--write" ]; then
        taze --write
    else
        taze
    fi;
