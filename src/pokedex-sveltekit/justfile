#!/usr/bin/env -S just --justfile

shebang := if os() == 'windows' { 'powershell.exe' } else { '/usr/bin/sh' }

alias r := run

# List available commands.
_default:
    just --list --unsorted

# Setup the repository.
setup:
    dprint --version || cargo install dprint

    pnpm i

# Run the app in development mode.
run:
    pnpm run dev

# Build the app.
build:
    pnpm run build

# Lint the codebase.
lint:
    pnpm run lint

# Format the codebase.
fmt:
    pnpm run format
    dprint fmt --config configs/dprint.json

# Check is the codebase properly formatted.
fmt-check:
    pnpm run format:check
    dprint check --config configs/dprint.json

# Tasks to make the code-base comply with the rules. Mostly used locally or in git hooks.
comply: fmt lint

# Check if the repository comply with the rules. Mostly used in CI.
check: fmt-check lint

# Create a new release. Example `just release v2.2.0`
release version:
    bash scripts/release.sh {{ version }}

# Check dependencies health. Pass `--write` to uppgrade dependencies.
up arg="":
    #!{{ shebang }}
    if [ "{{ arg }}" = "--write" ]; then
        taze --write
    else
        taze
    fi;
